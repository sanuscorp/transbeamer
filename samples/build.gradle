plugins {
    id 'application'
    id 'java'
    alias(libs.plugins.avro)
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(':lib')
    implementation libs.avro
    implementation libs.bundles.beam
}

// Create custom tasks for each sample application
def createSampleTask(String taskName, String mainClassName) {
    tasks.register(taskName, JavaExec) {
        description = "Run the $mainClassName application"
        mainClass = mainClassName
        classpath = sourceSets.main.runtimeClasspath
        group = "application"
    }
}

createSampleTask(
    'welcome',
    'com.sanuscorp.transbeamer.samples.Welcome'
)

createSampleTask(
    'createParquet',
    'com.sanuscorp.transbeamer.samples.CreateParquet'
)

createSampleTask(
    'convertParquetToCsv',
    'com.sanuscorp.transbeamer.samples.ConvertParquetToCsv'
)
tasks.named('convertParquetToCsv') {
    dependsOn 'createParquet'
}

createSampleTask(
    'toAvroAndParquet',
    'com.sanuscorp.transbeamer.samples.ConvertCsvToAvroAndParquet'
)

createSampleTask(
    'convertParquetToNDJson',
    'com.sanuscorp.transbeamer.samples.ConvertParquetToNDJson'
)
tasks.named('convertParquetToNDJson') {
    dependsOn 'createParquet'
}

createSampleTask(
    'convertNDJsonToCSV',
    'com.sanuscorp.transbeamer.samples.ConvertNDJsonToCSV'
)
tasks.named('convertNDJsonToCSV') {
    dependsOn('convertParquetToNDJson')
}